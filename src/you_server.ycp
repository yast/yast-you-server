/**
  
  YOU server configuration

  Author: Cornelius Schumacher <cschum@suse.de>

*/
{
    textdomain "online-update";

    import "Wizard";
    import "Runlevel";
    import "Require";
    import "Directory";
    import "Popup";
    import "Label";
    
    boolean server_is_running = true;

    list products = [];

    Wizard::CreateDialog();

    // show the special YOU image
    Wizard::SetImage( (byteblock)SCR::Read ( .local.byte, Directory::themedir +
					     "/current/online_update_long.png" ) );

    define void RunSuseConfigApache( string state ) ``{
	SCR::Write( .sysconfig.apache_youserver.YOU_SERVER, state );
	if( !SCR::Write( .sysconfig.apache_youserver, nil ) ) {
	    Popup::Error( _("Unable to write /etc/sysconfig/apache") );
	    return;
	}
	if( SCR::Execute( .target.bash, "/sbin/SuSEconfig --module apache" ) != 0 ) {
	    Popup::Error( _("'SuSEconfig --module apache' failed.") );
	}
    }

    define void StartYouServer() ``{
	UI::OpenDialog(
		       `opt(`decorated),
		       `VBox(
			     `HBox(
				   `HSpacing(1),
				   `VBox( 
					 `Left( `Label( `id( `state ), _("Starting YOU server...") ) ),
					 `Left( `Label( `id( `details ), _("Initializing...") ) )
					 ),
				   `HSpacing( 1 )
				   )
			     )
		       );

	string help = _("The YOU server requires an HTTP server. The apache package
will now be installed.");

	if ( !Require::AreAllPackagesInstalled( [ "apache" ] ) ) {
	    UI::ChangeWidget( `id(`details), `Value, _("Installing HTTP server...") );
	    UI::RecalcLayout();
    
	    if ( !Require::RequireAndConflict( [ "apache" ], [], help ) ) {
		Popup::Error (_("Failed to install required packages."));	
	    }
	}
	if ( Require::AreAllPackagesInstalled( [ "apache" ] ) )	{
	    UI::ChangeWidget( `id(`details), `Value, _("Adapting configuration...") );
	    UI::RecalcLayout();

	    RunSuseConfigApache( "yes" );

	    UI::ChangeWidget( `id(`details), `Value, _("Starting server...") );
	    UI::RecalcLayout();
	    integer exitCode = Runlevel::RunInitScript( "apache", "restart" );
	    if ( exitCode != 0 ) {
		Popup::Error( _("Error starting apache.") );
	    }   
	}
	UI::CloseDialog();
    }

    define void StopYouServer() ``{
	UI::OpenDialog(
		       `opt(`decorated),
		       `VBox(
			     `HBox(
				   `HSpacing(1),
				   `Label( `id( `state ), _("Stopping YOU server...") ),
				   `HSpacing( 1 )
				   )
			     )
		       );

	Runlevel::RunInitScript( "apache", "stop" );

	RunSuseConfigApache( "no" );

	UI::CloseDialog();
    }

    define void EnableSLP() ``{

	if ( !Require::AreAllPackagesInstalled( [ "openslp" ] ) )
	{
	    if ( !Require::RequireAndConflict( [ "openslp" ], [],
					      _("The installation of the package 'openslp' is required. ") ) )
	    {
		Popup::Error (_("Failed to install required packages."));	
	    }
	}

	if ( Require::AreAllPackagesInstalled( [ "openslp" ] ) )
	{
		integer exitCode = Runlevel::RunInitScript( "slpd", "start" );
		if ( exitCode != 0 )
		{
		    Popup::Error( _("Error starting slpd.") );
		    UI::ChangeWidget( `id(`slp), `Value, false );
		}
	}
        // SLP register
	if ( SCR::Execute( .target.bash, "slptool register" ) != 0 )
	{
	    Popup::Error( _("SLP register failed.") );
	}
	else
	{
	    y2milestone( "SLP registration succesful" ); 
	}
    }
    
    define void DisableSLP() ``{

	integer exitCode =  Runlevel::RunInitScript( "slpd", "stop" );
	if ( exitCode != 0 )
	{
	    Popup::Error( _("Cannot stop slpd") ); 
	    UI::ChangeWidget( `id(`slp), `Value, true ); 
	}
    }
    
    define map ProductDialog( map product ) ``{
	UI::OpenDialog(
		       `opt( `decorated ),
		       `VBox(
			     `VSpacing( 0.3 ),
			     `TextEntry( `id( `name ), _("Product Name"), product[ "name" ]:"" ),
			     `TextEntry( `id( `version ), _("Version"), product[ "version" ]:"" ),
			     `TextEntry( `id( `arch ), _("Architecture"), product[ "arch" ]:"" ),
			     `TextEntry( `id( `url ), _("Synchronization URL"),
					 product[ "url" ]:"" ),
			     `VSpacing( 0.3 ),
			     `HBox(
				   `PushButton( `id( `ok ), `opt( `default ), Label::OKButton() ),
				   `PushButton( `id( `cancel ), Label::CancelButton() )
				   )
			     )
		       );
    
	UI::SetFocus( `name );
    
	symbol ret = (symbol)UI::UserInput();

	if ( ret == `ok ) {
	    product[ "name" ] = (string)UI::QueryWidget( `id( `name ), `Value );
	    product[ "version" ] = (string)UI::QueryWidget( `id( `version ), `Value );
	    product[ "arch" ] = (string)UI::QueryWidget( `id( `arch ), `Value );
	    product[ "url" ] = (string)UI::QueryWidget( `id( `url ), `Value );
	} else {
	    product = nil;
	}

	UI::CloseDialog();
    
	return product;
    }

    define void SyncServer() ``{
	term productLabels = `VBox();

	UI::OpenDialog(
		       `opt(`decorated),
		       `VBox(
			     `HBox(
				   `HSpacing(1),
				   `VBox(
					 `Label( `id( `state ), _("Syncing Server...") ),
					 `Label( `id( `product ), "                   " )
					 ),
				   `HSpacing( 1 )
				   ),
			     `HBox(
				   `PushButton( `id( `close ), Label::CloseButton() )
				   )
			     )
		       );

	integer i = 0;
	while ( i < size( products ) ) {

	    map pm = products[ i ]:$[];
	    string pName = pm[ "name" ]:"";
	    if ( size( pName ) != 0 ) {
		string pVersion = pm[ "version" ]:"";
		string pArch = pm[ "arch" ]:"";
		string pUrl = pm[ "url" ]:"";
		string productName = pName + " " + pVersion + " " + pArch;
		UI::ChangeWidget( `id( `product ), `Value, productName );
		UI::RecalcLayout();

		string cmd = "online_update -G -p " + pName + " -v " + pVersion +
		    " -a " + pArch + " -u " + pUrl;

		y2debug( "%1", cmd );

		SCR::Execute( .target.bash, cmd );
	    }
      
	    i = i + 1;
	}

	UI::ChangeWidget( `id( `state ), `Value, _("Server synced.") );
	UI::RecalcLayout();

	map bashResult = (map)SCR::Execute( .target.bash_output, "date" );
	string lastSync = bashResult[ "stdout" ]:"";
	SCR::Write( .you.config.ServerLastSync, lastSync );
	SCR::Write( .you.config, nil );

	UI::UserInput();
    
	UI::CloseDialog();
    }

    term widget_server_unused =
	`HBox(
	      `Label( _("Status: unused") ),
	      `HStretch(),
	      `PushButton( `id( `server_control ), _("Start Server") )
	      );

    term widget_server_running =
	`HBox(
	      `Label( _("Status: running") ),
	      `HStretch(),
	      `PushButton( `id( `server_control ), _("Stop Server") )
	      );

    define void UpdateServerStatus() ``{
	integer httpd_status = Runlevel::ServiceStatus( "apache" );
	string sys_you_server = (string)SCR::Read( .sysconfig.apache_youserver.YOU_SERVER );

	if ( httpd_status == 0 && sys_you_server == "yes" )
	    server_is_running = true;
	else
	    server_is_running = false;

	if ( server_is_running ) {
	    UI::ReplaceWidget( `id( `server_status ), widget_server_running );
	} else {
	    UI::ReplaceWidget( `id( `server_status ), widget_server_unused );
	}
    }

    define void UpdateProductList() ``{
	list items = [];

	integer i = 0;
	while( i < size( products ) ) {
	    map p = products[ i ]:$[];
	    items = add( items, `item( `id( i ), p[ "name" ]:"", p[ "version" ]:"",
				       p[ "arch" ]:"", p[ "url" ]:"" ) );
	    i = i + 1;
	}

	UI::ChangeWidget( `id( `products ), `Items, items );
    }

    define void UpdateLastSync() ``{
	string lastSync = (string)SCR::Read( .you.config.ServerLastSync );
	if ( lastSync == nil || size( lastSync ) == 0 )
	    lastSync = _("No sync up to now.");
	UI::ChangeWidget( `id(`last_sync), `Value, lastSync );
	UI::RecalcLayout();
    }

    define void LoadSettings() ``{
	products = [];
	string settings = (string)SCR::Read( .target.string,
					     "/var/lib/YaST2/you/you_server_settings" );
	list lines = [];
	if ( size( settings ) < 0 )
	{
	    lines = splitstring( settings, "\n" );
	}
	integer i = 0;
	while( i < size( lines ) ) {
	    list e = splitstring( lines[ i ]:"", ";" );
	    string pname = e[ 0 ]:"";
	    if( size( pname ) != 0 ) {
		map p = $[ "name":e[ 0 ]:"", "version":e[ 1 ]:"", "arch":e[ 2 ]:"",
			   "url":e[ 3 ]:"" ];
		products = add( products, p );
	    }
	    i = i + 1;
	}
    }

    define void SaveSettings() ``{
	string settings = "";
	integer i = 0;
	while( i < size( products ) ) {
	    map p = products[ i ]:$[];
	    string line = p[ "name" ]:"" + ";" + p[ "version" ]:"" + ";" +
		p[ "arch" ]:"" + ";" + p[ "url" ]:"" + "\n";
	    y2internal( "%1", line );
	    settings = settings + line;
	    i = i + 1;
	}
    
	SCR::Write( .target.string, "/var/lib/YaST2/you/you_server_settings",
		    settings );
    }

    term contents =
	`VBox(
	      `VSpacing( 1.0 ),
	      `Frame ( _("Server &Administration"),
		       `VBox( `VSpacing(0.5),
			      `ReplacePoint( `id( `server_status ), `Empty() ),
			      `Left(`CheckBox( `id( `slp ), `opt(`notify), _("Enable/Disable &SLP"),
					       Runlevel::ServiceStatus( "slpd" ) == 0 )
				    ),
			      `VSpacing(0.5)
			      )
		       ),
	      `VSpacing( 0.5 ),
	      `Table( `id( `products ),
		      `header( _("Product"), _("Version"), _("Architecture"),
			       _("Synchronisation URL") ) ),
	      `HBox(
		    `PushButton( `id( `add ), _("Add") ),
		    `PushButton( `id( `edit ), _("Edit") ),
		    `HStretch(),
		    `Right(`PushButton( `id( `remove ), _("Remove")  ))
		    ),
	      `VSpacing( 1.0 ),
	      `HBox(
		    `Label( _("Last Synchronization: ") ),
		    `HStretch(),
		    `Label( `id(`last_sync), _("10 days ago") )
		    ),
	      `VSpacing( 0.5 ),
	      `HBox(
		    `PushButton( `id( `sync ), "Synchronize now" ),
		    `HStretch(),
		    `PushButton( `id( `setup_sync ), "Setup automatic synchronization" )
		    ),
      	      `VSpacing( 1.0 )
	      );

    string help_text = _("<p>This module is used to setup and control a local YOU
server.</p>");

    Wizard::ReplaceNextButton (`PushButton(`id(`next), Label::CloseButton()));
    Wizard::ReplaceAbortButton( `Empty() );
    Wizard::ReplaceBackButton( `Empty() );

    Wizard::SetContents( _("YaST Online Update Server Configuration"),
			 contents, help_text, false, true );

    UI::ChangeWidget( `id(`setup_sync), `Enabled, false );

    UpdateServerStatus();

    LoadSettings();

    if ( size( products ) == 0 ) {

	map you_info = Pkg::YouStatus();
	products = add( products, $[ "name": lookup( you_info, "product", "unknown"),
				     "version": lookup( you_info, "version", "unknown" ),
				     "arch": lookup( you_info, "basearch", "unknown" ),
				     "url": "ftp://ftp.suse.com/pub/suse" ] );
      
	y2debug( "Product information: %1", you_info );
    }

    UpdateProductList();
    UpdateLastSync();

    symbol ret = `next;

    repeat {

	ret = (symbol)UI::UserInput();

	if ( ret == `server_control ) {
	    if ( server_is_running ) {
		StopYouServer();
	    } else {
		StartYouServer();
	    }
	    UpdateServerStatus();
	} else if ( ret == `slp ) {
	    if ( (boolean) UI::QueryWidget(`id(`slp), `Value) ) {
		// check whether package "openslp" is installed and start slpd
		EnableSLP();
	    }
	    else {
		// stop slpd
		DisableSLP();
	    }
	} else if ( ret == `sync ) {
	    SyncServer();
	    UpdateLastSync();
	} else if ( ret == `add ) {
	    map product = ProductDialog( $[] );
	    if ( product != nil ) products = add( products, product );
	    UpdateProductList();
	} else if ( ret == `edit ) {
	    integer current = (integer)UI::QueryWidget( `id(`products), `CurrentItem );
	    map product = ProductDialog( products[ current ]:$[] );
	    if ( product != nil ) products[ current ] = product;
	    UpdateProductList();
	    UI::ChangeWidget( `id(`products), `CurrentItem, current );
	} else if ( ret == `remove ) {
	    integer current = (integer)UI::QueryWidget( `id(`products), `CurrentItem );
	    products = remove( products, current );
	    UpdateProductList();
	    if ( current >= size( products ) ) current = size( products ) - 1;
	    UI::ChangeWidget( `id(`products), `CurrentItem, current );
	}

    } until ( ret == `next || ret == `cancel  || ret == `abort );

    SaveSettings();

    UI::CloseDialog();

    return ret;
}
