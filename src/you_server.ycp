/**
  
  YOU server configuration

  Author: Cornelius Schumacher <cschum@suse.de>

*/
{
    textdomain "you-server";

    import "Wizard";
    import "Service";
    import "Require";
    import "Directory";
    import "Popup";
    import "Label";
    import "SLP";

    include "you_server_autosync.ycp";

    
    boolean server_is_running = true;
    
    list products = [];

    // SLP registration file (located in /etc/slp.reg.d)
    string reg_file = "you.reg";
    // alias for YOU base path (as set in /etc/httpd/you_server.conf)
    string base_path = "/YOU";
    // name for the service
    string service_type = "you.suse";
    // default name for the server
    string server_name = _("Local YOU Server");

    
    Wizard::CreateDialog();
    Wizard::SetDesktopIcon("you_server");

    define void RunSuseConfigApache( boolean enable_you_server ) ``{

	string flags = (string)SCR::Read( .sysconfig.apache2.APACHE_SERVER_FLAGS );

	if ( enable_you_server )
	{
	    // add flag "you_server" to APACHE_SERVER_FLAGS
	    flags = flags + " " + "you_server";
	}
	else	// remove "you_server"
	{
	    list<string> server_flags =  splitstring( flags, " ," );
	    server_flags = maplist( string flag, server_flags, { if ( flag != "you_server" ) return flag; } );
	    flags = mergestring( server_flags, " " );
	}
	SCR::Write( .sysconfig.apache2.APACHE_SERVER_FLAGS, flags );

	if( !SCR::Write( .sysconfig.apache2, nil ) ) {
	    Popup::Error( _("Unable to write /etc/sysconfig/apache2") );
	    return;
	}
	if( SCR::Execute( .target.bash, "/sbin/SuSEconfig --module apache2" ) != 0 ) {
	    Popup::Error( _("'SuSEconfig --module apache2' failed.") );
	}
    }

    define void StartYouServer() ``{
	boolean ok = true;

	UI::OpenDialog(
		       `opt(`decorated),
		       `VBox(
			     `HBox(
				   `HSpacing(1),
				   `VBox(
					 `Left( `Label( `id( `state ), _("Starting YOU server...") ) ),
					 `Left( `Label( `id( `details ), _("Initializing...") ) )
					 ),
				   `HSpacing( 1 )
				   )
			     )
		       );

	string help = _("The YOU server requires an HTTP server.
The 'apache2' package is required.
Install the package now?");

	if ( !Require::AreAllPackagesInstalled( [ "apache2, apache2-prefork" ] ) )
	{
	    boolean install = true;
	    if ( Service::Enabled( "apache" ) )
	    {
		install =  Popup::ContinueCancel( // text of a popup
						 _( "There is already the HTTP Server 'apache'
in use. The YOU server needs 'apache2'.
Do you want to disable 'apache' and install
and use 'apache2' or cancel the installation?  " ) );

		if ( install )
		    Service::Adjust( "apache", "disable" );
	    }

	    if ( install )
	    {
		UI::ChangeWidget( `id(`details), `Value, _("Installing HTTP server...") );
		UI::RecalcLayout();

		if ( !Require::RequireAndConflict( [ "apache2", "apache2-prefork" ], [], help ) )
		{
		    Popup::Error (_("Failed to install required packages."));
		    ok = false;
		}
	    }
	    else
	    {
		ok = false;
	    }
	}
	
	if ( ok )
	{
	    UI::ChangeWidget( `id(`details), `Value, _("Adapting configuration...") );
	    UI::RecalcLayout();

	    // add "you_server" to APACHE_SERVER_FLAGS
	    RunSuseConfigApache( true );
	    // adjust runlevel
	    Service::Adjust( "apache2", "enable" );
	    
	    UI::ChangeWidget( `id(`details), `Value, _("Starting server...") );
	    UI::RecalcLayout();
	    integer exitCode = Service::RunInitScript( "apache2", "restart" );
	    if ( exitCode != 0 ) {
		Popup::Error( _("Error starting apache.") );
	    }   
	}
	UI::CloseDialog();
    }

    define void StopYouServer() ``{
	// headline of a popup
	boolean restart =  Popup::AnyQuestion( _( "Question" ),
					    // text of a popup
					    // user decision required whether to stop or restart
					    _( "Do you want to shut down the HTTP Server
(apache2) or restart without
providing the YOU patches?" ),
					    _("&Restart"),
					    _("S&hut down"),
					       `focus_yes );

	string label = "";
	if ( restart )
	    label = _("Restarting YOU server ...");
	else
	    label = _("Stopping YOU server...");
	
	UI::OpenDialog(
		       `opt(`decorated),
		       `VBox(
			     `HBox(
				   `HSpacing(1),
				   `Label( `id( `state ), label ),
				   `HSpacing( 1 )
				   )
			     )
		       );


	// remove "you_server" from APACHE_SERVER_FLAGS
	RunSuseConfigApache( false );
	
	if ( restart )
	{
	    Service::RunInitScript( "apache2", "reload" );
	}
	else
	{
	    Service::RunInitScript( "apache2", "stop" );
	}

	UI::CloseDialog();
    }

    define boolean SLPRegistered() ``{
	if ( Service::Status( "slpd" ) == 0
	     && SCR::Read( .target.size,  "/etc/slp.reg.d/" + reg_file ) != -1 )
	    return true;
	else
	    return false;
    }
    
    define boolean EnableSLP() ``{

	string help = _( "The installation of the package 'openslp'
is required.
Install the package now?" );
	
	if ( !Require::AreAllPackagesInstalled( [ "openslp", "openslp-server" ] ) )
	{
	    if ( !Require::RequireAndConflict( [ "openslp", "openslp-server" ], [], help ) )
	    {
		Popup::Error (_("Failed to install required packages."));
		return false;
	    }
	}

	if ( !server_is_running )
	{
	    Popup::Warning (_("The HTTP server isn't running.
Please start the server first."));
	    UI::ChangeWidget( `id(`slp), `Value, false );
	    return false;  
	}

	boolean success = false;

	map bashResult = (map)SCR::Execute( .target.bash_output, "hostname -f" );
	string host = bashResult[ "stdout" ]:"";

	if ( size(host) != 0 )
	{
	    // SLP register - write file you.reg
	    string service = "service:" + service_type + ":http://" + substring( host, 0, size(host)-1 ) + base_path + ",en,65535";
	    success = SLP::RegFile ( service, $[ "description":server_name ], reg_file );
	}   

	if ( success )
	{
	    y2milestone( "Wrote /etc/slp.reg.d/%1", reg_file ); 

	    integer exitCode = Service::RunInitScript( "slpd", "restart" );

	    if ( exitCode != 0 )
		Popup::Error( _("Error starting slpd.") );
	}
	else
	{
	    Popup::Error( _("Error writing SLP registration file") );
	}
	
	if ( SLPRegistered() )
	{
	    UI::ChangeWidget( `id(`slp), `Value, true );
	    return true;
	}
	else
	{
	    UI::ChangeWidget( `id(`slp), `Value, false );
	    return false;
	}
    }
    
    define void DisableSLP() ``{
        // don't stop slpd - but restart after removing the .reg file

	boolean success = SLP::DeRegFile( "/etc/slp.reg.d/" + reg_file ); 

	if ( success )
	{
	    y2milestone( "/etc/slp.reg.d/%1 removed", reg_file );
	    integer exitCode = Service::RunInitScript( "slpd", "restart" );
	}
	else
	{
	    y2error( "Cannot remove /etc/slp.reg.d/%1", reg_file );
	}
	
	if ( SLPRegistered() )
	    UI::ChangeWidget( `id(`slp), `Value, true );
	else
	    UI::ChangeWidget( `id(`slp), `Value, false );
    }
    
    define map ProductDialog( map<string, string> product ) ``{

	string passwd_file = "/var/lib/YaST2/you/password";
	string content = (string)SCR::Read (.target.string, passwd_file);
	list<string> splitted = [];

	string user = "";
	string passwd = "";
	
	if ( content != nil ) {
	    splitted = splitstring( content, "\n");

	    foreach ( string entry, splitted, ``{
		// get user and password for this URL
		list<string> linesplit = splitstring( entry, "=" );
		string ident =  linesplit[0]:"";

		if ( substring( ident, 0, 9 ) == "USERNAME_"
		     && substring( ident, 9 ) == product["url"]:"" )
		{
		    user = linesplit[1]:"";
		}
		if ( substring( ident, 0, 9 ) == "PASSWORD_"
		     && substring( ident, 9 ) == product["url"]:"" )
		{
		    passwd = linesplit[1]:"";
		}    
	    } );
	}
  
	UI::OpenDialog(
		       `opt( `decorated ),
		       `VBox(
			     `VSpacing( 0.3 ),
			     `TextEntry( `id( `name ), _("Product Name"), product[ "name" ]:"" ),
			     `TextEntry( `id( `version ), _("Version"), product[ "version" ]:"" ),
			     `TextEntry( `id( `arch ), _("Architecture"), product[ "arch" ]:"" ),
			     `TextEntry( `id( `url ), _("Synchronization URL"), product[ "url" ]:"" ),
			     `VSpacing( 0.3 ),
			     `HBox(
				   `HSpacing( 0.5 ),
				   `Frame( _("Au&thentication"),
					   `VBox(
						 `Left( `CheckBox( `id( `anonymous ),
								   `opt( `notify ), _("&Anonymous"),
								   size( user ) == 0 ) ),
						 `HBox(
						       `TextEntry( `id( `username ), _("&User Name"), user )
						       ),
						 `HBox(
						       `Password( `id( `password ), _("&Password"), passwd )
						       )
						 )
					   )
				   ),
			     `VSpacing( 0.3 ),   
			     `HBox(
				   `PushButton( `id( `ok ), `opt( `default ), Label::OKButton() ),
				   `PushButton( `id( `cancel ), Label::CancelButton() )
				   )
			     )
		       );
    
	UI::SetFocus( `name );
    
	symbol ret = `any;
	
	repeat
        {
            boolean anonymous = (boolean) UI::QueryWidget( `id( `anonymous ), `Value );
	    UI::ChangeWidget( `id( `username ), `Enabled, !anonymous );
	    UI::ChangeWidget( `id( `password ), `Enabled, !anonymous );
		
            ret = (symbol)UI::UserInput();
	    
	    if ( ret == `ok ) {
		product[ "name" ] = (string)UI::QueryWidget( `id( `name ), `Value );
		product[ "version" ] = (string)UI::QueryWidget( `id( `version ), `Value );
		product[ "arch" ] = (string)UI::QueryWidget( `id( `arch ), `Value );
		product[ "url" ] = (string)UI::QueryWidget( `id( `url ), `Value );

		if ( !anonymous )
		{
		    // get user and passwd
		    string passwd = (string)UI::QueryWidget( `id( `password ), `Value );
		    string user = (string)UI::QueryWidget( `id( `username ), `Value );
               
		    string passwdstr = "PASSWORD_" + product[ "url" ]:"" + "=" + passwd;
		    string userstr = "USERNAME_" +  product[ "url" ]:"" + "=" + user;

		    string newcontent = "";
		    
		    if ( splitted == [] )
		    {
			newcontent = passwdstr + "\n" + userstr;
		    }
		    else
		    {
			foreach ( string entry, splitted, ``{
			    list<string> linesplit = splitstring( entry, "=" );
			    string ident =  linesplit[0]:"";

			    if ( substring( ident, 9 ) != product["url"]:"" )
			    {
				// write existing/unchanged entries
				newcontent = newcontent + entry + "\n";
			    }	
			} );
			// add new/changed entries
			newcontent = newcontent + passwdstr + "\n" + userstr;
		    }
		    
		    SCR::Write( .target.string, passwd_file, newcontent );
		}
	    }
	    
	} until ( ret == `cancel || ret == `ok );

	UI::CloseDialog();
    
	return product;
    }

    define void SyncServer() ``{
	term productLabels = `VBox();

	UI::OpenDialog(
		       `opt(`decorated),
		       `VBox(
			     `HBox(
				   `HSpacing(1),
				   `VBox(
					 `Label( `id( `state ), _("Syncing Server...") ),
					 `Label( `id( `product ), "                   " )
					 ),
				   `HSpacing( 1 )
				   )
			     )
		       );

	integer i = 0;
	list<string> errlist = [];
	boolean error = false;
	
	while ( i < size( products ) ) {

	    map pm = products[ i ]:$[];
	    string pName = pm[ "name" ]:"";
	    if ( size( pName ) != 0 ) {
		string pVersion = pm[ "version" ]:"";
		string pArch = pm[ "arch" ]:"";
		string pUrl = pm[ "url" ]:"";
		string productName = pName + " " + pVersion + " " + pArch;
		UI::ChangeWidget( `id( `product ), `Value, productName );
		UI::RecalcLayout();

		string cmd = "online_update -G -p " + "\"" + pName + "\"" + " -v " + pVersion +
		    " -a " + pArch + " -u " + pUrl;

		y2milestone( "%1", cmd );

		integer exitcode = (integer)SCR::Execute( .target.bash, cmd );
		if ( exitcode != 0 ) {
		    string product = pName + " " + pVersion + " " + pUrl;
		    errlist = add( errlist, product ); 
		    y2error( "Synchronization of %1 failed", product);
		    error = true;
                }
	    }
	    i = i + 1;
	}

	if ( error )
	{
	    UI::CloseDialog();
	    string errtext = "";
	    foreach( string product, errlist, ``{
		errtext = errtext + product + "\n";
	    });
	    // text of an error popup - list of all product which cannot be synchronized follows
	    Popup::Error( _("Synchronisation failed for:") + "\n" + errtext +
			  _("Check whether the requested server is up. If yes,
make sure that the user name and the password
are set correctly (see 'Edit' dialog).") );
	}
	else
	{
	    UI::ChangeWidget( `id( `state ), `Value, _("Server synced.") );
	    UI::RecalcLayout();

	    map bashResult = (map)SCR::Execute( .target.bash_output, "date" );
	    string lastSync = bashResult[ "stdout" ]:"";
	    SCR::Write( .you.config.ServerLastSync, lastSync );
	    SCR::Write( .you.config, nil );
    
	    UI::CloseDialog();
	}
    }

    term widget_server_unused =
	`HBox(
	      `Label( _("Server status: unused") ),
	      `HStretch(),
	      `PushButton( `id( `server_control ), _("Start Server") )
	      );

    term widget_server_running =
	`HBox(
	      `Label( _("Server status: running") ),
	      `HStretch(),
	      `PushButton( `id( `server_control ), _("Stop Server") )
	      );

    define void UpdateServerStatus() ``{
	integer httpd_status = Service::Status( "apache2" );
	string  sys_you_server = "";
	
	if ( httpd_status == 0 )
	    sys_you_server = (string)SCR::Read( .sysconfig.apache2.APACHE_SERVER_FLAGS );

	if ( httpd_status == 0
	     && find(sys_you_server, "you_server") != -1 )
	    server_is_running = true;
	else
	    server_is_running = false;

	if ( server_is_running ) {
	    UI::ReplaceWidget( `id( `server_status ), widget_server_running );
	} else {
	    UI::ReplaceWidget( `id( `server_status ), widget_server_unused );
	}
    }

    define void UpdateProductList() ``{
	list items = [];

	integer i = 0;
	while( i < size( products ) ) {
	    map p = products[ i ]:$[];
	    items = add( items, `item( `id( i ), p[ "name" ]:"", p[ "version" ]:"",
				       p[ "arch" ]:"", p[ "url" ]:"" ) );
	    i = i + 1;
	}

	UI::ChangeWidget( `id( `products ), `Items, items );
    }

    define void UpdateLastSync() ``{
	string lastSync = (string)SCR::Read( .you.config.ServerLastSync );
	if ( lastSync == nil || size( lastSync ) == 0 )
	    lastSync = _("No sync up to now.");
	UI::ChangeWidget( `id(`last_sync), `Value, lastSync );
	UI::RecalcLayout();
    }

    define void LoadSettings() ``{
	products = [];
	string settings = (string)SCR::Read( .target.string,
					     "/var/lib/YaST2/you/you_server_settings" );
	list lines = [];
	if ( settings != nil )
	{
	    lines = splitstring( settings, "\n" );
	}
	integer i = 0;
	while( i < size( lines ) ) {
	    list e = splitstring( lines[ i ]:"", ";" );
	    string pname = e[ 0 ]:"";
	    if( size( pname ) != 0 ) {
		map p = $[ "name":e[ 0 ]:"", "version":e[ 1 ]:"", "arch":e[ 2 ]:"",
			   "url":e[ 3 ]:"" ];
		products = add( products, p );
	    }
	    i = i + 1;
	}
    }

    define void SaveSettings() ``{
	string settings = "";
	integer i = 0;
	while( i < size( products ) ) {
	    map p = products[ i ]:$[];
	    string line = p[ "name" ]:"" + ";" + p[ "version" ]:"" + ";" +
		p[ "arch" ]:"" + ";" + p[ "url" ]:"" + "\n";
	    y2internal( "%1", line );
	    settings = settings + line;
	    i = i + 1;
	}
    
	SCR::Write( .target.string, "/var/lib/YaST2/you/you_server_settings",
		    settings );
    }

    define string EditDialog() ``{
	string srv_name = "";
	
	if ( SLPRegistered() )
	{
	    // get the server name if SLP is already registered
	    map attrs = SLP::GetAttrMap ( service_type );
	    srv_name = attrs["name"]:"";
	}
	
	UI::OpenDialog(
		       `opt( `decorated ),
		       `VBox(
			     `VSpacing( 0.3 ),
			     `TextEntry( `id( `name ), _("Server Name"), srv_name==""?server_name:srv_name ),
			     `VSpacing( 0.3 ),
			     `HBox(
				   `PushButton( `id( `ok ), `opt( `default ), Label::OKButton() ),
				   `PushButton( `id( `cancel ), Label::CancelButton() )
				   )
			     )
		       );
    
	UI::SetFocus( `name );
    
	symbol ret = (symbol)UI::UserInput();

	if ( ret == `ok )
	    srv_name = (string)UI::QueryWidget( `id( `name ), `Value );

	UI::CloseDialog();

	return srv_name;
    }
    
    term contents =
	`VBox(
	      `Frame ( _("Server &Control"),
		       `VBox(
			      `ReplacePoint( `id( `server_status ), `Empty() ),
			      `HBox( `CheckBox( `id( `slp ), `opt(`notify), _("&SLP registration enabled"),
						SLPRegistered()),
				     `HStretch(),
				     `PushButton( `id( `editname), _("Ed&it Name") )
				    )
			      )
		       ),
	      `Table( `id( `products ),
		      `header( _("Product"), _("Version"), _("Architecture"),
			       _("Synchronisation URL") ) ),
	      `HBox(
		    `PushButton( `id( `add ), _("&Add") ),
		    `PushButton( `id( `edit ), _("&Edit") ),
		    `HStretch(),
		    `Right(`PushButton( `id( `remove ), _("&Remove")  ))
		    ),
	      `Frame( _("Synchronisation"),
		      `VBox(
			    `HBox(
				  `Label( _("Last Synchronization: ") ),
				  `HStretch(),
				  `Label( `id(`last_sync), _("10 days ago") )
				  ),
			    `HBox(
				  `PushButton( `id( `sync ), "Synchronize now" ),
				  `HStretch(),
				  `PushButton( `id( `setup_sync ), "Setup automatic synchronization" )
				  )
			    )
		      )
	      );
    // help text for the YOU Server Configuration dialog
    string help_text = _("<p>This module is used to setup and control a local YOU
server.</p>")	+
	// help text continues
	_("<p>Enable the <b>SLP registration</b> to announce the YOU server via the 'Service Location Protocol'.") +
	// help text continues
	_("<p>Press<b> Synchronize now</b> to get the current patches for all products.</p>") +
	// help text continues
	_("<p>The YOU server is added to the server list of an YOU client if <b>SLP_ENABLED</b> in
'/etc/sysconfig/onlineupdate' is set to \"yes\".</p>");
	
    Wizard::SetNextButton(`next, Label::CloseButton() );
    Wizard::HideAbortButton();
    Wizard::HideBackButton();

    Wizard::SetContents( _("YaST Online Update Server Configuration"),
			 contents, help_text, false, true );


    UpdateServerStatus();

    LoadSettings();

    if ( size( products ) == 0 ) {

	UI::OpenDialog(
		       `opt(`decorated),
		       `VBox(
			     `HBox(
				   `HSpacing(1),
				   `Left( `Label( `id( `state ), _("Getting product information...") ) ),
				   `HSpacing( 1 )
				   )
			     )
		       );

        // get product information and set default url
	map you_info = Pkg::YouStatus();
	string url = "";

        string product = you_info[ "product" ]:"unknown";
	if ( product == "SuSE Linux" || product == "SUSE LINUX" )
	    url = "ftp://ftp.suse.com/pub/suse";	// box
	else
	    url = "http://sdb.suse.de";			// business products

	products = add( products, $[ "name": you_info["product"]:"unknown",
				     "version": you_info["version"]:"unknown",
				     "arch": you_info["basearch"]:"unknown",
				     "url": url ] );

	UI::CloseDialog();
	y2debug( "Product information: %1", you_info );
    }

    UpdateProductList();
    UpdateLastSync();

    symbol ret = `next;

    repeat {

	ret = (symbol)UI::UserInput();

	if ( ret == `server_control ) {
	    if ( server_is_running ) {
		StopYouServer();
	    } else {
		StartYouServer();
	    }
	    UpdateServerStatus();
	} else if ( ret == `slp ) {
	    if ( (boolean) UI::QueryWidget(`id(`slp), `Value) ) {
		// check whether package "openslp" is installed and start slpd
		EnableSLP();
	    }
	    else {
		// stop slpd
		DisableSLP();
	    }
	} else if ( ret == `sync ) {
	    SyncServer();
	    UpdateLastSync();
	} else if ( ret == `setup_sync ) {
	    SetupAutomatic( products );
	}
	else if ( ret == `add ) {
	    map product = ProductDialog( $[] );
	    if ( product != $[] ) products = add( products, product );
	    UpdateProductList();
	} else if ( ret == `edit ) {
	    integer current = (integer)UI::QueryWidget( `id(`products), `CurrentItem );
	    map product = ProductDialog( products[ current ]:$[] );
	    if ( product != $[] ) products[ current ] = product;
	    UpdateProductList();
	    UI::ChangeWidget( `id(`products), `CurrentItem, current );
	} else if ( ret == `remove ) {
	    integer current = (integer)UI::QueryWidget( `id(`products), `CurrentItem );
	    products = remove( products, current );
	    UpdateProductList();
	    if ( current >= size( products ) ) current = size( products ) - 1;
	    UI::ChangeWidget( `id(`products), `CurrentItem, current );
	} else if ( ret == `editname ) {
	    server_name = EditDialog();
	    // if SLP is already enabled, rewrite you.reg and restart slpd
	    if ( SLPRegistered() )
		EnableSLP();
	}

    } until ( ret == `next || ret == `cancel  || ret == `abort );

    SaveSettings();

    UI::CloseDialog();

    return ret;
}
